plugins {
	id "com.crystaelix.loom" version "1.13-SNAPSHOT"
	id "ploceus" version "1.13-SNAPSHOT"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "com.crystaelix.blossom" version "1.3.2"
	id "me.modmuss50.mod-publish-plugin" version "1.0.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://repo.cleanroommc.com/releases/"
			}
		}
		filter {
			includeGroup "zone.rong"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://jitpack.io/"
			}
		}
		filter {
			includeGroup "com.github.LegacyModdingMC.UniMixins"
		}
	}
}

def specVersion = "1"
def vendor = "thelm"
def corePluginClass = "com.crystaelix.mixinconbooter.MixinConBooterLoadingPlugin"

group = "com.crystaelix"
version = "1.3"

def archivesName = "mixinconbooter-legacy"
base.archivesName = archivesName

sourceSets {
	main {
		output.resourcesDir = java.destinationDirectory = layout.buildDirectory.dir("outputs/main")
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
		vendor = JvmVendorSpec.AZUL
	}
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
			property "fml.coreMods.load", corePluginClass
		}
		client {
			programArg "--username=Dev"
		}
		server
	}
	generatedIntermediateMappings()
}

ploceus {
	disableLibraryUpgrades()
}

dependencies {
	// Minecraft
	minecraft "com.mojang:minecraft:1.7.10"
	mappings loom.layered {
		mcp "de.oceanlabs.mcp:mcp_stable:12-1.7.10"
		mcp "net.minecraftforge:forge:1.7.10-10.13.4.1614-1.7.10:userdev"
	}
	exceptions ploceus.raven("1")
	signatures ploceus.sparrow("1")
	nests ploceus.nests("2")
	legacyForge "net.minecraftforge:forge:1.7.10-10.13.4.1614-1.7.10:universal"

	// Dependencies
	implementation("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:0.1.23") {transitive = false}
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml", "META-INF/neoforge.mods.toml", "fabric.mod.json"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": archivesName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"FMLCorePlugin": corePluginClass,
	"FMLCorePluginContainsFMLMod": "true"
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishing {
	publications {
		register("mavenJava", MavenPublication) {
			artifactId = archivesName
			artifact remapJar
			artifact sourcesJar
		}
	}
	repositories {
		if(project.hasProperty("CrystaelixMavenUsername") && project.hasProperty("CrystaelixMavenPassword")) {
			maven {
				name = "CrystaelixMaven"
				url = "https://maven.crystaelix.com/releases/"
				credentials(PasswordCredentials)
			}
		}
	}
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "legacy-${project.version}"
	changelog = file("changelog.md").text
	type = BETA
	modLoaders.add("forge")
	additionalFiles.from(sourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "1302886"
		minecraftVersionRange {
			start = "1.6.1"
			end = "1.7.10"
		}
		requires "unimixins"
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "boKqfS5e"
		minecraftVersionRange {
			start = "1.6.1"
			end = "1.7.10"
		}
		requires "unimixins"
	}
}
